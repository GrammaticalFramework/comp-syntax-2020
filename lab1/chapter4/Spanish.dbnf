-- utterances and sentences
Utt ::= Punct? Utt Punct # punct head punct
Utt ::= Conj Utt         # cc head
Utt ::= NP_obl comma Utt # cc punct head
Utt ::= parenth Utt parenth # punct head punct

Utt ::= S
Utt ::= QS
Utt ::= Imp
Utt ::= NP
Utt ::= AP
Utt ::= IP
Utt ::= IAdv
Utt ::= Num

-- sentences
S ::= NP? VP # nsubj head
S ::= NP? neg? have VP  # nsubj advmod aux head
S ::= NP? neg? aux have? VP  # nsubj advmod aux aux head

S ::= NP? neg? cop AdV? Comp # nsubj advmod cop advmod head
S ::= NP? neg? have AdV? cop Comp # nsubj advmod aux advmod cop head
S ::= NP? neg? aux AdV? have? cop Comp # nsubj advmod aux advmod aux cop head

S ::= NP? neg? cop AdV? VP_pass # nsubj:pass advmod aux:pass advmod head
S ::= NP? neg? have AdV? cop VP_pass # nsubj:pass advmod aux advmod aux:pass head
S ::= NP? neg? aux AdV? have? cop VP_pass # nsubj:pass advmod aux advmod aux aux:pass head

S ::= AdV? neg? cop AP SC? # expl advmod cop head ccomp

S ::= NP? be_V neg? SC # nsubj head advmod ccomp
S ::= NP? have neg? be_V SC # nsubj aux advmod head ccomp
S ::= NP? aux neg? have? be_V SC # nsubj aux advmod aux head ccomp

S ::= SC? VP # csubj head
S ::= SC? cop neg? VP_pass # csubj:pass aux:pass advmod head
S ::= SC? have neg? cop VP_pass # csubj:pass aux advmod aux:pass head
S ::= S_front NP? VS # parataxis nsubj head

QS ::= neg? aux NP? have? VP # advmod aux nsubj aux head
QS ::= neg? cop NP? Comp # advmod cop nsubj head

QS ::= neg? cop NP? VP_pass # advmod aux:pass nsubj:pass head
QS ::= neg? have NP? cop VP_pass # advmod aux nsubj:pass aux:pass head
QS ::= neg? aux NP? have? cop VP_pass # advmod aux nsubj:pass aux:pass aux head

QS ::= IP VP # nsubj head
QS ::= IP be_V # nsubj head
QS ::= IP neg? aux have? VP # nsubj advmod aux aux head
QS ::= IP neg? cop Comp # nsubj advmod cop head
QS ::= IP neg? aux have? cop Comp # nsubj advmod aux cop head
QS ::= IP neg? aux NP have? VPSlash # obj advmod aux nsubj aux head

QS ::= IP neg? cop VP_pass # nsubj:pass advmod aux:pass head
QS ::= IP neg? have cop VP_pass # nsubj:pass advmod aux aux:pass head
QS ::= IP neg? aux have? cop VP_pass # nsubj:pass advmod aux aux:pass aux head

QS ::= IAdv neg? aux NP have? VP # advmod advmod aux nsubj aux head
QS ::= IAdv neg? cop NP Comp # advmod advmod cop nsubj head
QS ::= IAdv neg? cop NP Comp # advmod advmod cop nsubj head

QS ::= IAdv cop NP neg? VP_pass # advmod aux:pass nsubj:pass advmod head

-- indirect questions
QS ::= if S # mark head
QS ::= IP NP does? neg? VPSlash # obj nsubj nsubj advmod head
QS ::= IP NP aux neg? have? VPSlash # obj nsubj aux advmod aux head
QS ::= IAdv NP aux neg? have? VP # advmod nsubj aux advmod aux head
QS ::= IAdv NP cop neg? Comp # advmod nsubj cop advmod head

RS ::= RP VP # nsubj head
RS ::= RP have neg? VP # nsubj aux advmod head
RS ::= RP cop neg? Comp # nsubj cop advmod head
RS ::= RP have neg? cop Comp # nsubj aux advmod cop head

RS ::= RP NP VPSlash # obj nsubj head
RS ::= NP VPSlash # nsubj head

-- imperatives

-- sentences with adverbials
S  ::= S_subcomma S # advcl head
S  ::= S S_sub # head advcl
S  ::= Adv_comma S # advmod head
S  ::= NP_oblcomma S # nmod head

-- embedded sentences
SC ::= QS # # 0.01
SC ::= VP_to
SC ::= S_that
SC ::= Prep S_that # case head

VP ::= V
VP ::= aux # # 0.001
VP ::= NP V2 # obj head
VP ::= V2 NP  # head obj
VP ::= V2 NP_obl # head obl
VP ::= V3 NP NP # head iobj obj
VP ::= V2C NP NP # head obj xcomp
VP ::= V2C NP AP # head obj xcomp
VP ::= VS S_that # head ccomp
VP ::= VV VP_to # head xcomp
VP ::= V2V NP VP # head obj xcomp
VP ::= V2V NP VP_to # head obj xcomp
VP ::= VP comma? Adv # head punct advmod
VP ::= AdV VP # advmod head
VP ::= VP V_part # head amod
VP ::= VP NP_obl # head obl # 0.001
VP ::= VS S_direct # head parataxis

VP_pass ::= V2
VP_pass ::= VP_pass Adv # head advmod
VP_pass ::= VP_pass NP_obl # head nmod

VPSlash ::= V2
VPSlash ::= VPSlash NP_obl  # head obl # 0.001
VPSlash ::= VPSlash Adv # head advmod
VPSlash ::= VP Prep # head case # 0.001

S_that ::= that? S # mark head

S_direct ::= colon S # punct head
S_direct ::= colon quote S quote # punct punct head punct
S_front  ::= S comma # head punct
S_front  ::= quote S quote comma # punct head punct punct

Comp ::= AP
Comp ::= NP
Comp ::= Adv
Comp ::= NP_obl
Comp ::= Comp comma? NP_obl # head punct nmod
Comp ::= Comp comma? Adv # head punct advmod

-- VP with to
VP_to ::= neg? to VP # advmod mark head
VP_to ::= neg? to cop Comp # advmod mark cop head

-- oblique noun phrase
NP_obl ::= Prep NP # case head

-- noun phrases
NP ::= PN
NP ::= Pron
NP ::= Num # # 0.1

NP ::= Det? Det? CN # det det head
NP ::= NP comma? NP_obl # head punct nmod
NP ::= Predet NP # det:predet head
NP ::= NP_poss CN # nmod:poss head
NP ::= Num NP # nummod head
NP ::= N PN # head flat
NP ::= NP SC # head acl
NP ::= NP comma? S_sub # head punct acl
NP ::= NP comma? RS # head punct acl:relcl
NP ::= Symb Num # head nummod
NP ::= NP comma? VP_pass # head punct acl # 0.01

NP_poss ::= NP 's # head case
NP_poss ::= Pron_poss

-- some very special rules ---
NP        ::= Det CN_quoted # det head
CN_quoted ::= quote CN quote # punct head punct
NP        ::= NP NP_appos # head appos # 0.01
NP_appos  ::= Punct_appos NP # punct head

Num ::= Card
Num ::= Digits

CN ::= N
CN ::= AP CN # amod head
CN ::= CN comma? AP # head punct amod # 0.01

PN ::= PN_one PN # head flat

N ::= N_one N # compound head
N ::= PN CN # compound head

Card ::= Card_one Card # compound head

IP ::= IDet CN # det head

-- adjectival phrases
AP ::= A
AP ::= AdA AP     # advmod head
AP ::= AP NP_obl  # head nmod

-- adverbials
-- Adv ::= Prep NP # case head

S_sub       ::= comma? Subj S # punct mark head
S_subcomma  ::= Subj S comma? # mark head punct
Adv_comma   ::= Adv comma? # head punct
NP_oblcomma ::= NP_obl comma? # head punct
S_sub       ::= comma? Subj VP # punct mark head # 0.1
S_subcomma  ::= Subj VP comma? # mark head punct # 0.1

-- coordination
S       ::= Conj_pre? ListS S_conj # cc:preconj head conj
ListS   ::= S
ListS   ::= ListS S_comma # head conj
S_conj  ::= comma? Conj S # punct cc head
S_comma ::= comma S # punct head

NP       ::= Conj_pre? ListNP NP_conj # cc:preconj head conj # 0.1
ListNP   ::= NP
ListNP   ::= ListNP NP_comma # head conj
NP_conj  ::= comma? Conj NP # punct cc head
NP_comma ::= comma NP # punct head

AP       ::= Conj_pre? ListAP AP_conj # cc:preconj head conj
ListAP   ::= AP
ListAP   ::= ListAP AP_comma # head conj
AP_conj  ::= comma? Conj AP # punct cc head
AP_comma ::= comma AP # punct head

VP       ::= Conj_pre? ListVP VP_conj # cc:preconj head conj # 0.9
ListVP   ::= VP
ListVP   ::= ListVP VP_comma # head conj
VP_conj  ::= comma? Conj VP # punct cc head
VP_comma ::= comma VP # punct head

-- in Adv conjunctions, we want to include NP_obl parts: all that matters for UD is the category of the first conjunct
Adv       ::= Conj_pre? ListAdv Adv_conj # cc:preconj head conj
ListAdv   ::= Adv
ListAdv   ::= ListAdv Adv_comma # head conj
Adv_conj  ::= comma? Conj Adv # punct cc head
Adv_conj  ::= comma? Conj NP_obl # punct cc head
Adv_comma ::= comma Adv # punct head
Adv_comma ::= comma NP_obl # punct head

NP_obl     ::= Conj_pre? ListNP_obl Adv_conj # cc:preconj head conj
ListNP_obl ::= NP_obl
ListNP_obl ::= ListNP_obl Adv_comma # head conj


-- POS tag configurations
#pos VERB V V2 V3 VS VV V2V V2C be_V
#pos NOUN N N_one
#pos ADJ A
#pos PROPN PN PN_one
#pos ADV Adv IAdv AdA AdV Predet neg
#pos SCONJ Subj that if
#pos CCONJ Conj Conj_pre
#pos DET Det IDet
#pos PRON Pron IP RP Pron_poss
#pos PART to 's
#pos PUNCT Punct Punct_appos comma quote colon parenth
#pos NUM Card Digits Card_one
#pos ADP Prep
#pos SYM Symb

-- structural word lexicon: normal categories
#token Prep a ante bajo cabe con contra de desde durante en enter hacia hasta mediante para por según sin so sobre tras versus vía
#token IP quién qué
#token IAdv cuándo dónde -- "por qué"
#token AdA muy demasiado genéticamente
#token AdV definitivamente siempre nunca solamente ya
#token Det un una unos unas el la las los cada alguno alguna algunos algunas mucho mucha muchos muchas ningún ninguno ninguna ningunos ningunas
#token Pron yo me tú te él ella se nosotros nosotras nos vosotros vosotras os ellos ellas
#token Subj si cuándo although aunque como
#token aux poder deber necesitar querer saber soler -- haber que, tener que, other verb forms...
#token Punct . ¡ ! ¿ ?
#token Punct_appos , :
#token Conj y o ni
#token Conj_pre ambos ambas o ni
#token IDet cuál cuáles
#token Pron_poss mi mis mío míos mía mías tu tus tuyo tuya tuyos tuyas su sus suyo suya suyos suyas nuestro nuestra nuestros nuestras vuestro vuestra vuestros vuestras -- mixed with possessive adjectives
#token RP cual cuales cuanto cuantos donde quien quienes cuyo cuyos que # el que, la que...
#token Card uno dos tres cuatro cinco -- etc.

-- structural word lexicon: syncategorematic words
#token cop ser soy eres es somos sois son estar estoy estás está estamos estáis están era fue sido estaba
#token be_V ser soy eres es somos sois son estar estoy estás está estamos estáis están era fue sido estaba
#token have haber he has ha hay hemos habéis han había habían habría -- and this is just pres. ind. ... (also, I'm not including "tener" because it's not used in that way)
#token neg no
#token that que -- ?
#token to que --?
#token if si
#token comma ,
#token colon :
#token quote "
#token parenth ( ) [ ]

-- content word lexicon
# token V_part deslizándose